# Auctions! (minus "max price" logic)
# https://github.com/chain-engineering/chainos/blob/master/docs/spec/p2c.md#auction-contract

contract AuctionAsset(priceAsset, bidPrice num, sellerScript, bidderScript, deadline num) {
  clause refund() {
    verify bidderScript == '';
    verify time() >= deadline;
    checkpredicate(sellerScript);
  }
  clause bid(price num, newBidderScript) {
    verify price > bidPrice;
    verify time() < deadline;
    if bidderScript != '' {
      verify reserveoutput(bidPrice, priceAsset, bidderScript);
    }
    verify reserveoutput(price, priceAsset, BidContract(asset(), amount(), sellerScript, newBidderScript, deadline));
    verify reserveoutput(amount(), asset(), AuctionAsset(priceAsset, price, sellerScript, newBidderScript, deadline));
  }
  clause claim() {
    verify time() >= deadline;
    verify reserveoutput(bidPrice, priceAsset, sellerScript);
    verify reserveoutput(amount(), asset(), bidderScript);
  }
}

contract BidContract(assetID, assetAmount num, sellerScript, bidderScript, deadline num) {
  clause release(newBidPrice num) {
    verify time() < deadline;
    verify reserveoutput(amount(), asset(), bidderScript);
    verify newBidPrice > amount();
    verify reserveoutput(newBidPrice, asset(), BidContract(assetID, assetAmount, sellerScript, bidderScript, deadline));
  }
  clause pay() {
    verify reserveoutput(amount(), asset(), sellerScript);
    verify reserveoutput(assetAmount, assetID, bidderScript);
  }
}
